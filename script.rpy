image coupleR:
    "images/title_couple_R.png"
image coupleG:
    "images/title_couple_G.png"
image coupleB:
    "images/title_couple_B.png"
image couple:
    "images/title_couple.png"

image hill:
    "images/bg/hill.png"
    zoom 0.55
    xpos 0.5
    ypos 1.25
    zoom 0.4

image river:
    "images/bg/river.png"
    zoom 0.6

image trees:
    "images/bg/trees.png"

image bridge:
    "images/bg/bridge.png"
    zoom 0.6

image space:
    "images/bg/space.jpg"
    zoom 0.35

image dagger:
    "images/dagger.png"

image her neutral:
    "her/her neutral.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side her neutral:
    "her/side her neutral.png"
    zoom 0.4

image her anxious:
    "her/her anxious.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side her anxious:
    "her/side her anxious.png"
    zoom 0.4

image her evasive:
    "her/her evasive.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side her evasive:
    "her/side her evasive.png"
    zoom 0.4

image her forlorn:
    "her/her forlorn.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side her forlorn:
    "her/side her forlorn.png"
    zoom 0.4

image her happy:
    "her/her happy.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side her happy:
    "her/side her happy.png"
    zoom 0.4

image her joy:
    "her/her joy.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side her joy:
    "her/side her joy.png"
    zoom 0.4

image her neutral talk:
    "her/her neutral talk.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side her neutral talk:
    "her/side her neutral talk.png"
    zoom 0.4

image her anxious talk:
    "her/her anxious talk.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side her anxious talk:
    "her/side her anxious talk.png"
    zoom 0.4

image her evasive talk:
    "her/her evasive talk.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side her evasive talk:
    "her/side her evasive talk.png"
    zoom 0.4

image her forlorn talk:
    "her/her forlorn talk.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side her forlorn talk:
    "her/side her forlorn talk.png"
    zoom 0.4

image her happy talk:
    "her/her happy talk.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side her happy talk:
    "her/side her happy talk.png"
    zoom 0.4

image her joy talk:
    "her/her joy talk.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side her joy talk:
    "her/side her joy talk.png"
    zoom 0.4

image not_me neutral:
    "not_me/not_me neutral.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side not_me neutral:
    "not_me/side not_me neutral.png"
    zoom 0.4

image not_me angry:
    "not_me/not_me angry.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side not_me angry:
    "not_me/side not_me angry.png"
    zoom 0.4

image not_me nervous:
    "not_me/not_me nervous.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side not_me nervous:
    "not_me/side not_me nervous.png"
    zoom 0.4

image not_me neutral talk:
    "not_me/not_me neutral talk.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side not_me neutral talk:
    "not_me/side not_me neutral talk.png"
    zoom 0.4

image not_me angry talk:
    "not_me/not_me angry talk.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side not_me angry talk:
    "not_me/side not_me angry talk.png"
    zoom 0.4

image not_me nervous talk:
    "not_me/not_me nervous talk.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side not_me nervous talk:
    "not_me/side not_me nervous talk.png"
    zoom 0.4

image not_me neutral silhouette:
    "not_me/not_me neutral talk.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7
    matrixcolor InvertMatrix(0.0)*ContrastMatrix(1.0)*SaturationMatrix(1.0)*BrightnessMatrix(-1.0)*HueMatrix(0.0)

image side not_me neutral silhouette:
    "not_me/side not_me neutral talk.png"
    zoom 0.4
    matrixcolor InvertMatrix(0.0)*ContrastMatrix(1.0)*SaturationMatrix(1.0)*BrightnessMatrix(-1.0)*HueMatrix(0.0)

image not_me angry silhouette:
    "not_me/not_me angry talk.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7
    matrixcolor InvertMatrix(0.0)*ContrastMatrix(1.0)*SaturationMatrix(1.0)*BrightnessMatrix(-1.0)*HueMatrix(0.0)

image side not_me angry silhouette:
    "not_me/side not_me angry talk.png"
    zoom 0.4
    matrixcolor InvertMatrix(0.0)*ContrastMatrix(1.0)*SaturationMatrix(1.0)*BrightnessMatrix(-1.0)*HueMatrix(0.0)

image not_me nervous silhouette:
    "not_me/not_me nervous talk.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7
    matrixcolor InvertMatrix(0.0)*ContrastMatrix(1.0)*SaturationMatrix(1.0)*BrightnessMatrix(-1.0)*HueMatrix(0.0)

image side not_me nervous silhouette:
    "not_me/side not_me nervous talk.png"
    zoom 0.4
    matrixcolor InvertMatrix(0.0)*ContrastMatrix(1.0)*SaturationMatrix(1.0)*BrightnessMatrix(-1.0)*HueMatrix(0.0)

## him
image him neutral:
    "him/him neutral.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side him neutral:
    "him/side him neutral.png"
    zoom 0.4

image him neutral talk:
    "him/him neutral talk.png"
    yanchor 40
    xanchor 0.25
    zoom 0.7

image side him neutral talk:
    "him/side him neutral talk.png"
    zoom 0.4


transform waytooclose:
    xpos -1200
    ypos -2400
    xzoom 5
    yzoom 5

transform centreclose:
    xpos 340
    ypos -355
    xzoom 1.5
    yzoom 1.5

transform centre:
    xpos 540
    xzoom 1

transform leftclose:
    xpos 54
    ypos -300
    xzoom 1.5
    yzoom 1.5

transform centreleft:
    xpos 297
    xzoom 1

transform offleft:
    xpos -816
    xzoom 1

transform left:
    xpos 54
    xzoom 1

transform leftflip:
    xpos 54
    xzoom -1

transform right:
    # Screenwidth = 1080
    xpos 1080-54
    xzoom 1

transform rightclose:
    # Screenwidth = 1080
    xpos 1080-54
    ypos -300
    xzoom 1.5
    yzoom 1.5

transform rightflip:
    # Screenwidth = 1080
    xpos 1080-54
    xzoom -1


image noise:
    function whitenoise
    "images/bg/noise1.png" with punch1
    pause 2
    "images/bg/noise2.png" with punch2
    pause 1.2
    "images/bg/noise3.png" with punch3
    pause 0.8
    "images/bg/noise4.png" with punch4
    pause 0.4
    "images/bg/noise5.png" with punch5
    pause 5.5
    pass

image stairs:
    subpixel True matrixcolor InvertMatrix(0.0)*ContrastMatrix(1.0)*SaturationMatrix(0.0)*BrightnessMatrix(0.0)*HueMatrix(0.0)

    function play_step
    "images/bg/stairs1.png" with Dissolve(0.25)
    pause 5
    function play_step
    "images/bg/stairs2.png" with Dissolve(0.25)
    pause 5
    function play_step
    "images/bg/stairs3.png" with Dissolve(0.25)
    pause 5
    function play_step
    "images/bg/stairs4.png" with Dissolve(0.25)
    pause 5
    function play_step
    "images/bg/stairs5.png" with Dissolve(0.25)
    pause 5
    function play_step
    "images/bg/stairs6.png" with Dissolve(0.25)
    pause 5
    function play_step
    "images/bg/stairs7.png" with Dissolve(0.25)
    pause 5
    repeat

image ctc:
    "gui/ctc.png"
    pause 1.5
    Null()
    pause 1.0
    repeat

image ctc_pause:
    "gui/ctc_pause.png"
    pause 1.0
    Null()
    pause 0.4
    repeat


define narrator = Character(ctc="ctc", ctc_pause="ctc_pause", ctc_timedpause=Null())
define n = Character(None, kind=nvl_narrator, ctc="ctc", ctc_pause="ctc_pause", ctc_timedpause=Null())
define me = Character(name=_("Me"), who_colour="#4f1c1b", what_prefix='"', what_suffix='"', ctc="ctc", ctc_pause="ctc_pause", ctc_timedpause=Null())
define her = Character(name=_("Her"), image="her", who_colour="#d258a1", what_prefix='"', what_suffix='"', ctc="ctc", ctc_pause="ctc_pause", ctc_timedpause=Null())
define him = Character(name=_("Him"), image="him", who_colour="#a8a999", what_prefix='"', what_suffix='"', ctc="ctc", ctc_pause="ctc_pause", ctc_timedpause=Null())
define not_me = Character(name=_("Not Me"), image="not_me", who_colour="#352526", what_prefix='"', what_suffix='"', ctc="ctc", ctc_pause="ctc_pause", ctc_timedpause=Null())

define config.menu_include_disabled = True
define MAX_AFFECT = 100
define DEAD_END_COUNT = 5
define LOCK_DEAD_ENDS = True

default persistent.true_end = False
default persistent.false_end = False
default persistent.affectation = MAX_AFFECT
default persistent.language_check = False

translate chinese python:
    f = "images/fonts/SJsuqian.ttf"
    gui.name_text_font = f
    gui.interface_text_font = f
    gui.text_font = f
    gui.button_text_font = f
    gui.choice_button_text_font = f
    me.name = "[[我]]"
    her.name = "[[她]]"
    not_me.name = "[[不是我]]"
    him.name = "[[他]]"

label splashscreen:
    if not persistent.language_check:
        scene title_screen_logoless
        menu:
            "Language: English":
                $ renpy.change_language(None, force=True)
            "{font=images/fonts/SJsuqian.ttf}本地化：简体中文{/font}":
                $ renpy.change_language("chinese", force=True)
        $ persistent.language_check = True
    return


init -1 python:

    if persistent.dead_ends is None:
        persistent.dead_ends = []
    if persistent.achievements is None:
        persistent.achievements = {k: False for k in ("end_false", "end_dead", "end_deadliest", "not_me", "end_true")}

    def merge_dead_ends(old, new, current):
        return sorted(set(old + new + current))

    def merge_achievements(old, new, current):
        for key, value in old:
            if value:
                current[key] = True
        for key, value in new:
            if value:
                current[key] = True
        return current

    renpy.register_persistent('dead_ends', merge_dead_ends)
    renpy.register_persistent('achievements', merge_achievements)

    punch1 = Move((15, 10), (-15, -10), .10, bounce=True, repeat=True, delay=.275)
    punch2 = Move((20, 13), (-20, -13), .11, bounce=True, repeat=True, delay=.275)
    punch3 = Move((30, 18), (-30, -18), .12, bounce=True, repeat=True, delay=.275)
    punch4 = Move((45, 25), (-45, -25), .14, bounce=True, repeat=True, delay=.275)
    punch5 = Move((90, 45), (-90, -45), .15, bounce=True, repeat=True, delay=.275)

    def whitenoise(tras, st, at):
        renpy.play("audio/whitenoise.wav", channel="sound")

    def play_step(tras, st, at):
        renpy.play("audio/step stap.wav", channel="sound")

    def play_slam(tras, st, at):
        renpy.play("audio/slam.wav", channel="sound")

    def cheat(value=0):
        persistent.affectation = value
        persistent.false_end = True

    def cheat_affectation(value):
        persistent.affectation = value
        persistent.affectation = min(MAX_AFFECT, persistent.affectation)
        persistent.affectation = max(0, persistent.affectation)
        update_layers()
    update_cheat_affectation = ui.adjustment(range=100.0, value=100.0, adjustable=True, changed=cheat_affectation)

    def add_achievement(name):
        achievement.grant(name)
        persistent.achievements[name] = True
        achievement.sync()

    def add_dead_end(number):
        add_achievement("end_dead")

        old_length = len(persistent.dead_ends)
        persistent.dead_ends.append(number)
        persistent.dead_ends = list(set(persistent.dead_ends))
        if persistent.achievements["end_deadliest"]:
            return

        new_length = len(persistent.dead_ends)
        if new_length >= DEAD_END_COUNT:
            add_achievement("end_deadliest")
        elif new_length > old_length:
            achievement.progress("end_deadliest", new_length)

    def do_nothing():
        return

    def reset_persistent():
        persistent.dead_ends = []
        persistent.true_end = False
        persistent.false_end = False
        persistent.affectation = MAX_AFFECT
        persistent.achievements = {k: False for k in ("end_false", "end_dead", "end_deadliest", "not_me", "end_true")}

    def adjust_affectation(add):
        if not persistent.false_end:
            persistent.affectation = MAX_AFFECT
            return
        persistent.affectation += add
        persistent.affectation = min(MAX_AFFECT, persistent.affectation)
        persistent.affectation = max(0, persistent.affectation)
        update_layers()
        renpy.block_rollback()

    def is_subtext_active():
        return persistent.false_end and persistent.affectation <= 75

    def dropped_pretense():
        return persistent.affectation <= 0

    def get_subtext_colour():
        if persistent.affectation >= 100:
            return "f0f0", "fff0"
        elif persistent.affectation >= 75:
            return "f0f0", "000a"
        elif persistent.affectation >= 25:
            return "f0fa", "000f"
        else:
            return "f0ff", "000f"

    def get_affect_colour():
        if persistent.affectation >= 100:
            return "00ff"
        elif persistent.affectation >= 75:
            return "00fa"
        elif persistent.affectation >= 25:
            return "00f7"
        else:
            return "00f3"

    def st(tag, argument, contents):
        colour, outline = get_subtext_colour()

        results = [(renpy.TEXT_TAG, u"color={}".format(colour))]
        results.append((renpy.TEXT_TAG, u"outlinecolor={}".format(outline)))
        if not is_subtext_active():
            results.append((renpy.TEXT_TAG, u's'))
            results.append((renpy.TEXT_TAG, u'noalt'))
            results.append((renpy.TEXT_TAG, u"cps=*30"))

        for kind, content in contents:
            results.append((kind, content))

        if not is_subtext_active():
            results.append((renpy.TEXT_TAG, u"/cps"))
            results.append((renpy.TEXT_TAG, u"/s"))
            results.append((renpy.TEXT_TAG, u"/noalt"))
        results.append((renpy.TEXT_TAG, u"/color"))
        results.append((renpy.TEXT_TAG, u"/outlinecolor"))
        return results

    def affect(tag, argument, contents):
        colour = get_affect_colour()
        results = [(renpy.TEXT_TAG, u"color={}".format(colour))]
        results.append((renpy.TEXT_TAG, u"outlinecolor=000f"))
        results += contents
        results.append((renpy.TEXT_TAG, u"/color"))
        results.append((renpy.TEXT_TAG, u"/outlinecolor"))
        return results

    def update_name():
        if is_subtext_active():
            config.name = _("com__et")
        else:
            config.name = _("comphet")


    def update_layers(delay=1):
        if persistent.affectation >= 100:
            layer1a = 1
            layer1b = 1
            layer2 = 0
            layer3 = 0
            layer4 = 0
        elif 100 > persistent.affectation > 75:
            scale = (persistent.affectation - 75.0) / 25.0
            layer1a = 1
            layer1b = 0.6 + (scale * 0.4)
            layer2 = 0.7 - (scale * 0.7)
            layer3 = 0
            layer4 = 0
        elif persistent.affectation == 75:
            layer1a = 1
            layer1b = 0.6
            layer2 = 0.7
            layer3 = 0
            layer4 = 0
        elif 75 > persistent.affectation > 25:
            scale = (persistent.affectation - 25.0) / 50.0
            layer1a = 1.0 - (0.5 * scale)
            layer1b = 0.6 - (0.3 * scale)
            layer2 = 0.7 + (0.3 * scale)
            layer3 = 0.3 * scale
            layer4 = 0
        elif persistent.affectation == 25:
            layer1a = 0.5
            layer1b = 0.3
            layer2 = 1
            layer3 = 0.3
            layer4 = 0
        elif persistent.affectation > 0:
            scale = persistent.affectation / 25.0
            layer1a = 0.5 * scale
            layer1b = 0.3 * scale
            layer2 = 1
            layer3 = 0.3 + (0.7 * scale)
            layer4 = 1 * scale
        else:
            layer1a = 0
            layer1b = 0
            layer2 = 1
            layer3 = 1
            layer4 = 1

        renpy.music.set_volume(layer1a, delay=delay, channel='layer1a')
        renpy.music.set_volume(layer1b, delay=delay, channel='layer1b')
        renpy.music.set_volume(layer2, delay=delay, channel='layer2')
        renpy.music.set_volume(layer3, delay=delay, channel='layer3')
        renpy.music.set_volume(layer4, delay=delay, channel='layer4')

    def start_layers(delay=3):
        if renpy.music.get_playing("layer1a"):
            return
        renpy.music.play("audio/music/SoNice1b.wav", channel='layer1b', loop=True, synchro_start=True, fadein=delay)
        renpy.music.play("audio/music/SoNice1a.wav", channel='layer1a', loop=True, synchro_start=True, fadein=delay)
        renpy.music.play("audio/music/SoNice2.wav", channel='layer2', loop=True, synchro_start=True, fadein=delay)
        renpy.music.play("audio/music/SoNice3.wav", channel='layer3', loop=True, synchro_start=True, fadein=delay)
        renpy.music.play("audio/music/SoNice4.wav", channel='layer4', loop=True, synchro_start=True, fadein=delay)

    def stop_layers(delay=None):
        renpy.music.stop(channel='layer1a', fadeout=delay)
        renpy.music.stop(channel='layer1b', fadeout=delay)
        renpy.music.stop(channel='layer2', fadeout=delay)
        renpy.music.stop(channel='layer3', fadeout=delay)
        renpy.music.stop(channel='layer4', fadeout=delay)

    renpy.music.register_channel("layer1a", "music")
    renpy.music.register_channel("layer1b", "music")
    renpy.music.register_channel("layer2", "music")
    renpy.music.register_channel("layer3", "music")
    renpy.music.register_channel("layer4", "music")
    config.custom_text_tags["st"] = st
    config.custom_text_tags["affect"] = affect

    achievement.register("end_deadliest", stat_max=5)

label start:
    nvl clear
    stop music
    $ stop_layers()
    stop sound
    hide screen ending
    $ update_name()
    $ update_layers()
    if persistent.false_end:
        show screen affect_meter

    jump begin

label trailer:
    scene hill

    show her happy talk
    her "Sooo what are You thinkin' about?"
    show her happy
    me "Uhhh..."

    menu:
        "Nothing":
            show her happy talk
        "My untimely death":
            $ adjust_affectation(-10)
            show her happy talk
        "You":
            $ adjust_affectation(-25)
            show her joy talk

    her "Oh yeah, Your {cps=*.5}faaaavourite{/cps} topic."
    show her happy

    #CUT

    show her evasive at left
    n "She pushes Me off {st}with a hint of genuine irritation in Her voice,{w=0.2} but it's so brief I can't get a real read{/st} of Her."
    n "I mumble a sorry, laughing {st}to hide My awkwardness, as I try to understand Her.{w=0.2} I feel like I can't just ask Her, when I know I'm always hiding from Her too.{w=0.2} It's safer for both of Us to be this way.{w=0.2}{/st}{nw}"
    n "{st}Instead of guarding My own secrets while sniffing out Hers{/st} awkwardly."
    nvl clear

    # CUT

    menu:
        "(sweating profusely) \"There's no deal, no deal at all!! He and I are just friends!\"":
            pass
        "(bashful) \"I don't know what that's supposed to mean, there's nothing special between Him and Me.\"":
            pass
        "(tired) \"There's {st}not really {/st}a deal, He and I {st}are just friends. No matter how many times He asks{/st}.\"" if is_subtext_active():
            pass
        "(frustrated) \"It shouldn't {st}be ambiguous{/st}, I {st}DON'T{/st} like Him. It's {st}just{/st} hard {st}to get that across to Him {/st}I guess." if is_subtext_active() and LOCK_DEAD_ENDS and 1 not in persistent.dead_ends:
            pass

    # CUT

    scene stairs with None
    n "It was always a running joke, {st}but they're tired of joking now.{w=0.1} My mother seems exasperated, like I've been putting off a chore.{w=0.2}{/st}{nw}"
    n "{st}My dad 'reminds' Me that guys \"Can't wait forever\", and a prize catch like Him could get snapped up any minute.{w=0.1}{/st}{nw}"
    n "{st}My sister thinks He's great, and tells Me how much worse Her boyfriend is, while fishing to ask if He's done something like cracking rude jokes{/st} at My expense."
    n "He's {st}done nothing {i}wrong{/i}. Just...{/st} been pleasant."

    # CUT

    scene space with None

    if _preferences.language == "chinese":
        $ title = "{st} 假{/st}\n结局"
    else:
        $ title = _("{st}FALSE{/st}\n END")
    show screen ending(title)
    show her evasive talk at offleft
    her "If You do want something else"
    show her neutral talk
    extend ", please tell Me."
    if _preferences.language == "chinese":
        $ title = " 假\n结局"
    else:
        $ title = _("FALSE\n END")

    show screen ending(title) with dissolve
    nvl clear
    n "Because I know what I want."

    $ persistent.false_end = True
    show noise:
        blend "add"
    $ adjust_affectation(-60)
    pause 5
    hide screen ending

    # CUT

    scene hill with None
    show her anxious at centre

    menu:
        "\"Uhm. Sorry for almost killing you?\"":
            pass
        "\"Just like in My japanese animes...\"":
            pass
        "*{st}blush{/st},{st} shake pathetically{/st} and {st}fumble for words{/st}*" if is_subtext_active():
            pass
        "(failing {st}to be suave{/st}) \"{st}Check{/st}....... {st}mate{/st}.\"" if is_subtext_active():
            $ adjust_affectation(-25)

    show her joy talk
    "She shoves Her hand in My face, laughing deep. A real, honest laugh {st}that warms My heart, even if it is{/st} at My expense."

    # CUT

    show her anxious talk
    her "Fuck... why did You have to wait to be so certain {i}now{/i}?{w} After..."

    hide screen affect_meter with None
    scene black

    stop sound
    play sound "audio/stab.wav" volume 3.0
    pause 1.0
    play music "<loop 8>audio/music/DeadEnd.wav"
    pause 1.5
    scene space with Dissolve(3.0)

    "As the sound rings out, it takes only an instant for the world to disappear. Then...{w} there's another voice. Crisp and clear."

    show not_me angry silhouette:
        subpixel True xpos 25 ypos -828 zoom 3.0

    not_me "Enough of Your melodramatic indecision.{w} Delaying the inevitable answer just hurts everyone else."

    show not_me:
        subpixel True
        parallel:
            xpos 25
            ease 0.8 xpos -1475
    with Pause(0.5)
    show not_me:
        pos (-1475, -828)

    not_me "If You actually wanted anything different,{w} You wouldn't have agonised over it so long."

    if _preferences.language == "chinese":
        $ title = "死亡\n结局\n#1"
    else:
        $ title = _("DEAD\n END\n  #1")
    show screen ending(title)
    show noise:
        blend "add"
    extend " So just accept where everyone else knows this is all going."
    window hide dissolve


    show not_me:
        subpixel True
        parallel:
            xpos -1475
            ease 1.0 xpos -2835


    pause 7
    return

label begin:
    scene hill
    $ start_layers()

    n "She sits on top of the railing, Her legs dangling over the side with the steep drop.{w} Her fearlessness is both impressive and deeply reckless to Me."
    show her evasive at centre with dissolve
    n "I simply could never."
    n "She's holding on to it,{w} but I could not stop the thoughts of how... {w}precarious it is."
    n "It's not {b}meant{/b} to be sat on, that's what the bench is for.{w} So how could I trust it to be safe?"
    n "What if it just gave way under My weight?{w} The worst part wouldn't be dropping into the void."
    n "It'd be the knowledge that My family would tut disapprovingly,{w} saying how{nw}"
    n "'I wasn't careful enough',"
    extend "\n'I shouldn't have leaned on such a flimsy rail'."
    extend "\n'No-one {i}else{/i} in the family would be so foolish...'"
    nvl clear

    show her happy talk
    her "Sooo what are You thinkin' about?"
    show her happy
    me "Uhhh..."

    menu:
        "Nothing":
            show her happy talk
        "My untimely death":
            $ adjust_affectation(-10)
            show her happy talk
        "You":
            $ adjust_affectation(-25)
            show her joy talk

    her "Oh yeah, Your {cps=*.5}faaaavourite{/cps} topic."
    show her happy

    "She pokes Me playfully {st}and I blush{/st}, before I gently brush Her hand away.{w} And- Huh?"
    "Behind Her, I see what almost looks like{w} a descending flash of blinding light?{w} I blink, trying to process it as I point down into the void below Us."
    me "What was that?"
    show her happy talk
    her "You think that bad a ploy is going to work?"
    show her happy
    "I shake My head, pointing insistently.{w} Maybe I'm {i}also{/i} using it as a way to distract from My airheadedness...{w=.3} But it {b}was{/b} real!"
    me "No really, something weird fell.{w}\nIt was like...{w} a comet?"
    "She's shaking Her head before I'm finished talking,"
    show her anxious with dissolve
    extend " but when She hears the last word, She finally turns around to look."

    show her evasive talk
    her "Com{st}ph{/st}et... {st}it's{/st} here{st} {b}now{/b}...{/st} ?"
    show her evasive
    "Her flat tone tells Me She's sceptical{st}, though I can tell from Her body language that's just a front.{w=0.2} Something is up with Her{/st} as She stares into the void."
    "I stare too, wondering if She's spotted anything. But there's no sign of whatever mysterious flash I saw."
    "I give up quickly, {st}becoming distracted by Her. I find Myself too busy staring,{/st} when She turns back to Me again, rolling Her eyes."
    show her forlorn talk
    her "At this time of day?{w} Somehow doubt that was a {i}comet{/i}."
    show her forlorn

    "She leans in close, putting a hand on My shoulder and giving Me a look {st}that stops My heart dead and freezes My body as I wait to see what She wants{/st} to say."
    show her forlorn talk
    her "Are You just trying to distract Yourself from Your date with Him?"
    show her forlorn

    menu:
        "(bashful) \"Maybe... a little...\"":
            $ adjust_affectation(5)
            $ forlorn = True
        "(denial) \"It's not a date!\"":
            $ adjust_affectation(5)
            $ forlorn = True
        "(sincere) \"It's not a date...\"":
            $ adjust_affectation(-5)
            $ forlorn = False
        "(resigned) \"I wish it was{st}n't{/st} a date...\"":
            if is_subtext_active():
                $ adjust_affectation(-15)
                $ forlorn = False
            else:
                $ forlorn = True
                $ adjust_affectation(5)

    if forlorn:
        show her forlorn talk
        her "Ha ha. Thought you'd say that."
        show her forlorn
    else:
        show her joy talk
        her "Ahh, so You're still not willing to be totally honest even with just Us here?"
        show her joy
        "Her eyes challenge Me, as if daring Me to admit more than even that.{w} {st}My mouth is dry, as I try to unpack which kind of honesty She wants from Me.{w=0.2} But... because I can't tell,{/st} I say nothing."

    "She pats My cheek before leaning away {st}and leaving Me disappointed.{w=0.2} I even catch a look in Her eyes, a familiar one that tells Me She's also hiding something{/st} from Me."
    show her forlorn talk
    her "You and Him are so cute.{w} You and Him have been like that for what, a decade?"
    show her forlorn

    menu:
        "\"Me and Him grew up together, I'm closer to Him than anyone.\"":
            $ adjust_affectation(10)
            $ evasive = True

        "\"Me and Him grew up together, He's like a brother to Me.\"":
            $ adjust_affectation(-5)
            $ evasive = True

        "\"Me and Him grew up together, so {st}He thinks{/st} He knows Me better than I know Myself.\"":
            if is_subtext_active():
                $ adjust_affectation(-10)
                $ evasive = False
            else:
                $ evasive = True

        "\"I{st} honestly don't know either{/st}.\"" if is_subtext_active():
            $ adjust_affectation(-20)
            $ evasive = False


    if evasive:
        show her evasive talk
        her "Guess there's no way to catch up, He must know all the embarrassing stories about You."
        show her evasive
        "I balk at the thought and shake My head."
        me "He saw all the phases of teen Me, He knows {i}far{/i} too much."
        "She nods, {st}even though there are parts of Me She knows. And He'll never have that{/st} understanding."
        show her happy talk
        her "All the phases, even Your LARPing one, right?"
        show her happy
        me "Even the LARPing!"
    else:
        show her anxious
        n "She looks at Me conflicted.{w} She's always been cagey about the relationship between Me and Him.{w} I guess because I always am too."
        n "I never say it, but I make it pretty clear{w} I don't want to talk about Him.{w} I'd rather talk about {st}Her all day more than{/st} anything else."
        n "I know She's asking because She's concerned, it's clearly on My mind a lot."
        nvl clear

        n "My feelings about Him are {st}so messy and difficult. I find Her easier to talk to than anyone else.{w=0.2} It's so natural to get in a rhythm and chat about anything, stupid or serious.{w}{nw}{/st}"
        n "{st}But with Him specifically, it's hard to go there because it's so much more{/st} complicated.{w} And I don't want to burden Her with that mess."
        n "It's nothing {st}but My bed, I have to lie in it.{w=0.2} I've basically been putting it off as long as I can remember.{w=0.2} It would be selfish to say it has anything{/st} to do with Her."
        nvl clear

        show her anxious talk
        her "Bet He knows {cps=*.3}aaall{/cps} sorts of secrets You won't reveal."
        show her anxious
        me "Of course not, I tell You everything!"
        "She regards Me sceptically."
        show her happy talk
        her "Oh really?{w} Then how come He's the one who said You were big into LARPing once upon a time?"
        show her happy
        me "Me?{w} What?{w} No...{w} Me?"
        "I sweat profusely, My body betraying My lie as Her face erupts into a big grin."

    show her joy talk at centre
    with vpunch
    her "Oh My god, it IS real??"
    play sound "audio/stab blunt.wav"
    show her at centre
    with hpunch
    extend " I've gotta see it."
    play sound "audio/stab blunt.wav"
    extend " There must be photos, right?"
    show her joy

    me "No pictures survived, not a single one!!"
    n "She stares Me down, with a wide grin on Her face.{w} She takes out Her phone and I dread what She's about to search for,{w=.3} so I grab at Her arm to take the phone away."
    n "She giggles as I wrestle with Her.{w} Her damned iron grip defying My efforts as I try to pull at Her two handed."

    n "And then{w} instead of pulling{w} I'm actually pushing Her."
    hide her
    scene black
    with dissolve
    show her anxious at leftclose
    with dissolve

    n "She starts to tip too far over the railing, Her eyes widening.{w} My mind reels with panic as My arms now scramble at Her and pull Her back from the void."
    n "Without Her fighting Me, Our combined strength easily keeps Her from falling."
    n "And swiftly leads to Her toppling onto Me, flattening Me out on the ground underneath Her."
    nvl clear

    n "I'm winded and breathless as She steadies Herself on top of Me."
    n "Both of Us pant, the panic slowly abating with each breath."
    n "Leaving Us in an awkward heap {st}that I never want to leave{/st} but I clear My throat to speak..."
    nvl clear

    scene hill
    hide her
    with dissolve

    show her anxious at centre

    menu:
        "\"Uhm. Sorry for almost killing you?\"":
            $ adjust_affectation(-5)
            $ deflect = True
        "\"Just like in My japanese animes...\"":
            $ adjust_affectation(-5)
            $ deflect = True
        "*{st}blush{/st},{st} shake pathetically{/st} and {st}fumble for words{/st}*" if is_subtext_active():
            $ adjust_affectation(-15)
            $ deflect = False
        "(failing {st}to be suave{/st}) \"{st}Check{/st}....... {st}mate{/st}.\"" if is_subtext_active():
            $ adjust_affectation(-25)
            $ deflect = False

    if deflect:
        show her evasive talk
        her "Oh shove {i}off{/i}."
        show her evasive at left
        n "She pushes Me off {st}with a hint of genuine irritation in Her voice,{w=0.2} but it's so brief I can't get a real read{/st} of Her."
        n "I mumble a sorry, laughing {st}to hide My awkwardness, as I try to understand Her.{w=0.2} I feel like I can't just ask Her, when I know I'm always hiding from Her too.{w=0.2} It's safer for both of Us to be this way.{w=0.2}{/st}{nw}"
        n "{st}Instead of guarding My own secrets while sniffing out Hers{/st} awkwardly."
        nvl clear
        her "If that's Your attempt to be cool, it's obvious why the deal with You and Him is {b}still{/b} unsettled."
    else:
        show her joy talk
        "She shoves Her hand in My face, laughing deep. A real, honest laugh {st}that warms My heart, even if it is{/st} at My expense."
        her "Well ok, if that's Your attempt to be cool, it's clear see why the deal with Him is {b}still{/b} ambiguous."
        show her joy
        "{st}I notice that{/st} She doesn't get up off of Me,{w} {st}but I don't draw attention to that, happy to stay like this as long as She'll let Me. Instead{/st} I respond."

    menu:
        "(sweating profusely) \"There's no deal, no deal at all!! He and I are just friends!\"":
            $ adjust_affectation(25)
            jump icecream_time
        "(bashful) \"I don't know what that's supposed to mean, there's nothing special between Him and Me.\"":
            $ adjust_affectation(10)
            jump icecream_time
        "(tired) \"There's {st}not really {/st}a deal, He and I {st}are just friends. No matter how many times He asks{/st}.\"" if is_subtext_active():
            $ adjust_affectation(-30)
            jump detour
        "(frustrated) \"It shouldn't {st}be ambiguous{/st}, I {st}DON'T{/st} like Him. It's {st}just{/st} hard {st}to get that across to Him {/st}I guess." if is_subtext_active() and LOCK_DEAD_ENDS and 1 not in persistent.dead_ends:
            $ adjust_affectation(-100)
            jump dead_end1

label detour:
    show her joy
    "She reaches out to put a hand on My cheek and {st}the touch is so electric I don't even notice when{/st} I gasp."
    "She recoils like She's touched an electric fence. Then begs a question with Her {st}hesitation. She knows the answer already, even if I've never told Her with words, but I have shown it with My{/st} eyes."
    show her evasive talk
    her "Don't answer lightly, have You really thought it through..."
    show her anxious
    "She looks {st}intently at Me, making it clear that this is an important question to Her, which means it's important{/st} to Me."

    menu:
        "(contrite) \"You're right, I should ask if He remembers what He said all those years ago...\"":
            $ adjust_affectation(10)
            show her forlorn
            "She pats My arm, {st}in an empty gesture.{w=0.2} It's easier that way, and She does it{/st} without recoiling."
            jump icecream_time

        "({st}looking into Her eyes{/st}) \"I guess I'm just afraid... of My feelings not being reciprocated.\"":
            if is_subtext_active():
                $ adjust_affectation(-10)
            else:
                $ adjust_affectation(5)
        "({st}sarcastic{/st}) \"You're right, {st}a decade is really not enough to settle {/st}My feel{st}ings on {/st}Him.\"" if is_subtext_active():
            $ adjust_affectation(-30)

        "({st}defiant{/st}) \"{st}I think {/st}I've known Him long{st} enough to say He's a bit of a dick{/st}.\"" if is_subtext_active() and LOCK_DEAD_ENDS and 2 not in persistent.dead_ends:
            $ adjust_affectation(-100)
            jump dead_end2

    show her anxious
    "She looks at Me carefully, gauging My feelings{st} and reading between the lines.{w=0.1} She knows I don't like to talk{/st} about Him."
    if is_subtext_active():
        "She speaks faintly, Her lips barely even moving."
    else:
        "She speaks faintly, Her lips barely even moving. If I wasn't so close to Her, I would miss Her next words."
    show her anxious talk
    her "{st}Think {i}carefully{/i}.{w=0.1} Choose right.{w=0.2} {/st}Right{st} for {b}You{/b}{/st}..."
    jump icecream_time

label icecream_time:
    n "I haven't known Her that long, definitely not as long as the decade I've known Him."
    n "But She has {st}changed My life. Meeting Her took a weight off My shoulders.{w=0.1} One I didn't realise was on Me.{w=0.2}{/st}{nw}"
    n "{st}She actually knew Him first, and that's how the two of Us met. I think He intended Her to be His 'wingman', which is hilarious.{w=0.1} But I can hardly fault the attempt. She has{/st} always been nice to everybody."
    n "So who wouldn't like Her?"
    nvl clear

    n "She gives Me a look{st}, in a way that only I can read.{w=0.1} One of My favourite things about Her is how She gives 'looks'.{w=0.1} A picture is worth a thousand words, but Her looks are short stories.{w=0.1}{/st}{nw}"
    n "{st}Sometimes I can't always parse them entirely,{w=0.1} but when We get to debrief later, it's a delight to unpack what I did and didn't pick up on.{w=0.1} In this moment, it's clear that She wants to talk...{w=0.1}{/st}{nw}"
    n "{st}But in private?{w=0.1} Even though... We are in private aren't We?{w=0.1} The thought that someone could technically be present... chills Me.{w=0.1} Especially the thought that somehow She noticed even though I didn't.{w=0.1}{/st}{nw}"
    n "{st}Noticing My expression, She gives Me a reassuring smile,{w} the kind that tells Me She'll take care of whatever the problem is.{w=0.1} My anxious heart calms down as She focuses on Me{/st} cheerily and says..."
    nvl clear

    show her happy talk at left
    her "Let's get some ice cream!"
    show her happy
    "She offers Me Her {st}soft{/st} hand, {st}and I clam up. My eyes fixate both on the hand{/st} and Her {st}pleading{/st} smile."

    menu:
        "(independent) \"I'm fine by Myself, but thanks.\"":
            $ adjust_affectation(25)
            jump continue_thread

        "(platonic) I take Her hand, what are {b}{i}best buds{/i}{/b} for?":
            $ adjust_affectation(40)
            jump continue_thread

        "({st}romantic{/st}) I clasp {st}Her {/st}hand {st}as if My life depended {/st}on it." if is_subtext_active():
            $ adjust_affectation(-30)
            jump offramp

        "(clumsy) I grasp for {st}Her hand earnestly and say{/st}..." if is_subtext_active() and LOCK_DEAD_ENDS and 3 not in persistent.dead_ends:
            $ adjust_affectation(-100)
            jump dead_end3

label continue_thread:
    scene hill

    n "I take Her hand as She lifts Me to My feet. She gives Me a warm smile {st}that looks almost genuine.{w=0.1} She's always been good at effortlessly faking a good mood, in a way I can't.{w=0.1}{/st}{nw}"
    n "{st}I open My mouth, suddenly full of regret, but I've squandered any time I had to speak{/st} before She turns away."
    nvl clear

    $ stop_layers(5.0)

    hide her with dissolve
    show her at offscreenleft

    scene stairs with dissolve

    n "She leads the way off to the steps down.{w} This has been such a nice day already, She is so nice. It's always nice to hang out with Her."
    n "She's the nicest friend I have, {st}I love Her{/st}.{w} And now We're going out for nice ice cream, {st}I wish there wasn't dread in the pit of My stomach so it could just feel{/st} nice!"
    n "As We start Our descent, My mind drifts to Him. I feel guilty for a moment thinking of Her as My nicest friend {st}even though it's obviously true{/st}."
    n "But that's right, isn't it? I adore Him, I love Him {st}as a friend{/st}!{w} He isn't My best friend {st}and not just because of how intense He can be{/st}."
    n "He's My oldest friend. Back when He and I were kids, everyone always joked {st}without joking{/st} about how close He and I were.{w} And that He and I would end up being {cps=*0.95}Together Forever{/cps}."
    nvl clear

    scene stairs with None
    n "I'd play with Him, He'd tease Me. But I obviously still liked it! I {st}'{/st}wouldn't still be friends with Him{st}'{/st} if I {i}didn't{/i} like it {st}is what everyone kept telling Me{/st}."
    n "He and I lived next to each other, always walked to school together. He'd always wait to walk Me home too. He'd get upset if I didn't wait for Him to walk Me home."
    n "Then I'd feel guilty. He was always waiting for Me, {st}and if He would never give up waiting{/st} shouldn't I just wait for Him too?"
    nvl clear

    scene stairs with None
    n "My family all still ask about 'the deal' with Him. They already know {st}what the answer is, they decided it long ago{/st}. And They're just waiting for Me to realise {st}and accept the \"correct\" answer{/st}."
    n "My mom especially loves to say 'That's what I thought when I was Your age' and then chuckles {st}in a hollow way.{w=0.2} The sound of that defeated laugh makes Me wish that My mother had listened{/st} to herself."
    nvl clear

    scene stairs with None
    n "It was always a running joke, {st}but they're tired of joking now.{w=0.1} My mother seems exasperated, like I've been putting off a chore.{w=0.2}{/st}{nw}"
    n "{st}My dad 'reminds' Me that guys \"Can't wait forever\", and a prize catch like Him could get snapped up any minute.{w=0.1}{/st}{nw}"
    n "{st}My sister thinks He's great, and tells Me how much worse Her boyfriend is, while fishing to ask if He's done something like cracking rude jokes{/st} at My expense."
    n "He's {st}done nothing {i}wrong{/i}. Just...{/st} been pleasant."
    extend "\n\ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ friendly."
    extend "\n\ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ patient."
    n "What more could I {st}be allowed to{/st} want?"
    nvl clear

    scene stairs with None
    n "He first confessed that He loved Me when I was 12."
    n "I never really thought about boys that way, I thought He was playing pretend.{w} I confessed back, and then I played house for the afternoon."
    n "It was fun, playing along with the joke His and My parents had told for years."
    n "He didn't find it funny when He realised I was playing.{w} I was only 12, so I just laughed."
    n "I didn't know yet to have guilt over how I felt.{w} I learned that when I was 15."
    nvl clear

    scene stairs with None
    n "The second time He confessed that He loved Me was when I was 15."
    n "There was some dance coming up.{w} I wasn't paying it much attention.{w} He asked Me to go, I said yes.\n{w}As a friend."

    n "Everyone knew except Me. My parents still just laughed at Me being 'oblivious'. Back then, they hadn't run out of patience with Me yet."
    n "I realised that the first confession was real,"
    extend "\n\ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ the date was real"
    extend "\n\ \ \ \ \ \ \ \ \ \ \ \ \ \ \ the second confession was real,"
    extend "\nThat second confession happened during the date."

    n "He had roses, some rehearsed speech, and a pleading look.{w} I honestly don't remember it really, it barely feels like I was there."
    n "I told Him it was really serious and I'd need to think.{w} So I did.{w} A lot."
    nvl clear

    scene stairs with None
    n "The third time He confessed that He loved Me was when I was 18."
    n "It was a lot less rehearsed, more passionate.{w} This time, I do remember Him saying it was My last chance."
    n "I was going to college in another town. He was willing to try long distance for My sake,{w} but He needed Me to {st}'{/st}admit{st}'{/st} that I wanted it."
    n "I told Him it was still too big,"
    extend "\n\ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ too much,"
    extend "\n\ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ too soon."
    n "He stormed out, and that was it. In a way, it was a relief to get closure, to have that tension finally gone."
    nvl clear

    scene stairs with None
    n "The fourth time He confessed that He loved Me was when I was 19."
    n "He didn't use words.{w} He just had a girlfriend when I got back from college.{w} My family all talked about Her as 'the other woman'.{w} I was happy for Him to be free of the lifelong tension like I was."
    n "He introduced Me to Her. And even as He introduced Her, He was begging Me with His eyes to take Her place.{w} We hung out all Summer."
    n "After a fantastic Summer, I went back to college."
    n "He broke up with Her before I came home for Christmas."
    nvl clear

    scene stairs with None
    n "The fifth time He confessed that He loved Me{w} hasn't happened.{w}.. yet."
    nvl clear

    scene river with None
    $ start_layers(0)
    show her forlorn at centre with None

    "My attention snaps back to reality,{w} and suddenly I realise that I'm in the middle of a conversation with Her, but I haven't been listening."
    "My ice cream is half eaten, and She was telling a story but paused and is just looking at Me, waiting for a reaction."
    me "Oh... no way."
    "She cocks an eyebrow, shaking Her head at Me {st}sympathetically but also... frustrated{/st} because She can clearly tell I'm ruminating again."
    show her forlorn talk
    her "You're so distracted thinking about Him."
    show her forlorn
    "Her face wears a grin, catching Me{st}.{w=0.2} Seeing the expression behind Her grin, I feel compelled to apologise. But, I can't get the words {/st}out."
    "She's not wrong, I {i}was{/i} distracted thinking about Him.{w} But not in the way She thinks!!{w} {st}I wish I was thinking more about Her,{/st} but She won't believe that."

    menu:
        "\"Well... maybe a little bit.\"":
            $ adjust_affectation(20)
        "\"Oh My god, am I that transparent?\"":
            $ adjust_affectation(10)
        "\"No... {st}what if I said{/st}...{st} I was thinking about You{/st}...\"" if is_subtext_active() and persistent.affectation < 50 and LOCK_DEAD_ENDS and 4 not in persistent.dead_ends:
            $ adjust_affectation(-100)
            jump dead_end4

    show her happy
    n "There's a moment where Her face twitches {st}into a frozen expression{/st} as She looks up at someone behind Me."
    n "For a moment dread takes Me, as if {st}some unexpected horror was approaching.{w=0.1} But that was just foolishness, after all this is a very very expected horror.{w=0.2}{/st}{nw}"
    n "{st}I've known for a decade now that{/st} I have something to fear."
    n "She is {st}drifting into the background, not interfering with what's about to happen,{w=0.1} because I chose this.{w=0.2} I did choose this, didn't I?{w=0.1} After all it is still a choice to be{/st} silent."
    hide her
    with dissolve
    nvl clear
    jump false_end

label false_end:
    show him neutral at centre
    with dissolve
    n "He walks up{fast}."
    extend "\n\ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ I wave{fast}."

    show him neutral:
        subpixel True
        parallel:
            xpos 540 zoom 1.0
            ease 0.6 xpos 140 zoom 2.0
        parallel:
            ypos 0
            easeout 0.2 ypos -270
            easein 0.4 ypos -495
        parallel:
            blur 0.0
            ease 0.6 blur 4.0
    with Pause(0.6)
    show him neutral:
        pos (140, -495) zoom 2.0# blur 4.0

    n "He hugs Me{fast}."
    extend "\n\ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ I let Him{fast}."
    show him neutral talk:
        subpixel True
        parallel:
            xpos 140 zoom 2.0 blur 0.0
            ease 0.60 xpos -211 zoom 3.0 blur 10.0
        parallel:
            ypos -495
            easeout 0.2 ypos -900
            easein 0.4 ypos -1269
        parallel:
            blur 4.0
            ease 0.6 blur 40.0
    with Pause(0.70)
    show him neutral:
        pos (-211, -1269) zoom 3.0

    n "He wants to talk{fast}."
    extend "\n\ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ I nod{fast}."

    show him neutral talk:
        subpixel True
        parallel:
            blend 'normal' xpos -211 zoom 3.0 blur 40.0
            linear 0.60 blend None xpos -1120 zoom 5.0 blur 100.0
        parallel:
            ypos -1269
            linear 0.2 ypos -2000
            linear 0.4 ypos -2511
        parallel:
            blur 40.0
            ease 0.6 blur 100.0
    with Pause(0.60)
    show him neutral:
        blend None pos (-1120, -2511) zoom 5.0

    n "His words are polite{fast}."
    show him neutral
    extend "\n\ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ I won't remember them{fast}."

    show him neutral:
        subpixel True
        parallel:
            xpos -1120 zoom 5.0
            linear 0.60 xpos -2524 zoom 8.0
        parallel:
            ypos -2511
            linear 0.23 ypos -3523
            linear 0.37 ypos -4347
    with Pause(0.70)
    show him neutral:
        pos (-2524, -4347) zoom 8.0

    n "He looks expectant{fast}."
    extend "\n\ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ I say yes{fast}."
    nvl clear

    scene black

    show him neutral:
        pos (-2524, -4347) zoom 8.0 blur 100.0
        subpixel True
        parallel:
            easeout 15.0 pos (-7915, -11970) zoom 20.0
        parallel:
            blur 100.0 matrixcolor InvertMatrix(0.0)*ContrastMatrix(1.0)*SaturationMatrix(1.0)*BrightnessMatrix(0.0)*HueMatrix(0.0)
            easeout 15.00 blur 500.0 matrixcolor InvertMatrix(0.0)*ContrastMatrix(0.0)*SaturationMatrix(0.0)*BrightnessMatrix(0.0)*HueMatrix(0.0)
        parallel:
            alpha 1.0
            easeout 30.00 alpha 0.0

    n "He's overjoyed{fast}."
    extend "\n\ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ I{fast}..."

    n "My mother will say I {b}'ran out of excuses'{/b}."
    extend "\nMy father will say I {b}'teased Him long enough'{/b}."
    extend "\nMy sister will say I {b}'finally saw sense'{/b}."
    n "I{st} didn't have a choice.{w=0.1} Or, I kept having My choices taken away.{w=0.2}{/st}{nw}"
    n "{st}Asked once, it's a choice. If they never stop asking, it's a test.{w=0.1} I realised I didn't have the option of even failing the test.{w=0.1} I would take it again and again until I made a choice {/st}like Him."
    nvl clear

    $ stop_layers(3)
    scene space
    with Dissolve(3.0)

    n "And\ \n{w=1}{nw}"
    extend "\ \ \ \ that\n{w=1}{nw}"
    extend "\ \ \ \ \ \ \ \ \ was\n{w=1}{nw}"
    extend "\ \ \ \ \ \ \ \ \ \ \ \ \ that.\n"
    n "Everyone knew it {st}'{/st}had{st}'{/st} to end up this way."

    hide screen affect_meter

    if _preferences.language == "chinese":
        $ title = "{st}假{/st}\n结局"
    else:
        $ title = _("{st}FALSE{/st}\n END")

    show screen ending(title)
    pause
    show her forlorn talk at offleft
    her "If this is what makes You happy, then I'm glad for You."
    show her evasive
    her "But..."
    show her evasive talk
    her "That's {b}if{/b} it makes You happy."
    her "If You do want something else"

    show her neutral talk
    extend ", please tell Me."

    if _preferences.language == "chinese":
        $ title = "假\n结局"
    else:
        $ title = _("FALSE\n END")
    show screen ending(title) with dissolve
    nvl clear
    n "Because I know what I want."


    $ add_achievement("end_false")
    $ persistent.false_end = True
    show noise:
        blend "add"
    pause 5
    stop sound
    stop music
    $ stop_layers()
    jump start

label offramp:
    n "I'm not really sure what the act is for, but I grip Her tight as I climb to My feet.{w} Whatever's going on, I know I can trust Her with My life."

    show her anxious:
        subpixel True
        parallel:
            xpos 54
            ease 0.05 xpos 54
            ease 0.45 xpos 189
        parallel:
            ypos 0
            ease 0.05 ypos 45
            ease 0.40 ypos -258
            function play_slam
            ease 0.05 ypos -306
        parallel:
            zoom 1.0
            ease 0.05 zoom 1.0
            ease 0.40 zoom 1.4
            ease 0.05 zoom 1.5
    with Pause(0.5)
    with vpunch

    show her anxious:
        pos (189, -306) zoom 1.5

    n "Before I'm even steady on My feet, She forcefully slams Me down against the ground behind Me."
    n "I gasp for air, looking into Her eyes.{w} Rather than Her usual warmth, there's{w} a steely resolve in them, a sense of deep concentration."
    n "She keeps Me pinned, My body hanging limp and at Her mercy.{w} Her eyes look away from Me, into the distance somewhere across the street and She swears."
    n "She never swears {st}unless it's serious{/st}..."
    nvl clear

    n "I cough as My lungs refill with air,{w} My body in panic mode{w} but My brain is disconnected from any of that as I stare up at Her."
    n "Vaguely I feel like I should be shocked at seeing Her act so different to normal."
    n "Instead My mouth hangs open like I'm a fish She's hooked. I shake Myself, insisting I have to find out what the hell She's thinking."
    nvl clear

    me "Wh{w}-hh{w}-ah{w}-uhhhm?"
    "...maybe My brain is also in panic mode."

    show her anxious:
        subpixel True
        xpos 189
        ease 0.11 xpos 157
        ease 0.22 xpos 756
    with Pause(0.33)
    with hpunch
    show her anxious:
        xpos 756

    "I get no time to recover as She forcibly pulls Me to the side and{nw}"

    show dagger:
        subpixel True ypos 90 zoom 5.0
        parallel:
            xpos -2061
            linear 0.15 xpos 2088
        parallel:
            blur 5.0
            linear 0.07 blur 5.0

    play sound "audio/stab blunt.wav"

    "I get no time to recover as She forcibly pulls Me to the side and{fast} an explosion of dust pops from where We just were."

    me "FUCK!"
    "I cry out eloquently as She leans back to look Me over, Her cold eyes now softened with worry."
    show her anxious talk
    her "Did You get hit?"
    "I shake My head silently as She jumps onto the railing and looking down into the abyss."
    show her neutral talk
    her "Good, now's Our chance.{w} Let's get out of here."
    show her neutral
    me "Fuh?"
    "I ask incoherent and She shakes Her head. I could've said any word in that moment and She'd have understood."
    show her neutral talk
    her "Explanations can wait, just come on."

    show her neutral talk:
        subpixel True
        parallel:
            xpos 756
            ease 0.6 xpos 1368
        parallel:
            ypos -306
            ease 0.33 ypos -194
            ease 0.27 ypos 99
        parallel:
            zoom 1.5
            ease 0.60 zoom 0.5

    "She hops around to hang from the other side of the railing.{w} She stretches out her hand, waiting for Me to follow."

    menu:
        "\"Um... are you crazy??\"" if LOCK_DEAD_ENDS and 5 not in persistent.dead_ends:
            $ adjust_affectation(-100)
            jump dead_end5
        "\"No slow down, You HAVE to explain what's going on here.\"" if LOCK_DEAD_ENDS and 5 not in persistent.dead_ends:
            $ adjust_affectation(-100)
            jump dead_end5
        "Seeing the concern on Her face, I follow.":
            $ adjust_affectation(-15)
        "I run after Her without hesitation":
            $ adjust_affectation(-25)

    scene trees with dissolve
    play sound "audio/run.wav" loop
    "I clumsily hop over, She catches Me from tumbling too far. I blush, feeling embarassed as She clutches Me in a princess carry."
    "She is doing surprisingly well, navigating this steep cliff nimbly {i}while{/i} carrying Me.{w} I had no idea She was strong enough for that."
    me "Have You been working out?"
    "She cocks an eyebrow at Me."
    me "Ahm{w=.2}be{w=.2}no{w=.2}cause{w=.2}Ijust{w=.2}never{w=.2}nope"
    "She laughs as I verbally struggle,{w} not missing a beat as She carries Me further down."

    show dagger:
        subpixel True ypos -324 zoom 4.5 rotate 180 blur 5.0
        parallel:
            xpos 2088
            linear 0.15 xpos -2088


    "And something whizzes by My head, too fast to catch what it is."
    me "...maybe We can talk about that later."
    her "Let's."
    "She weaves Us both through the branches and roots, descending closer to the bottom.{w} More blurs fly past, narrowly missing Us."

    scene black
    queue sound "audio/run.wav" volume 0.75
    queue sound "audio/run.wav" volume 0.5
    queue sound "audio/run.wav" volume 0.15

    "I close My eyes, overwhelmed. I can barely keep track of where We are, let alone whatever is coming at Us."
    "However She{w}- She dances around in a rehearsed path, like She knows exactly where each and every attack is coming from.{w} I open My eyes to look up at Her."

    stop sound
    scene river
    show her anxious at centreclose
    "As We finally take a breather and settle down, there's a long pause. It seems We're not under fire at this moment."
    me "Are We in the clear?"
    show her forlorn talk
    her "Not a chance, this is just the start."
    show her forlorn
    "She shakes Her head, looking down at Me in her arms.{w} Her eyes evaluate Me for a moment."

    show her evasive talk
    her "How many... loops have You done?"
    show her evasive
    me "Uh.{w=.3} Are you...{w} trying to ask how old I am?"
    show her evasive talk
    her "No-{w} I mean loops,{w=0.2} {b}time loops{/b}.{w} How many times have You repeated all this?"
    show her evasive
    "My blank expression answers Her question for Me."
    show her neutral talk
    her "Wait, this is...{w} Your first?"
    show her neutral
    "Even though I still don't understand, I am compelled to feel self conscious as she sets me down.{w} I feel shaky, now standing on My own two feet again."
    "She puts a hand on My shoulder quickly to reassure Me."
    show her forlorn talk
    her "There's nothing wrong with that. Just-{w} You did so well following Our path. So it was natural to {i}assume{/i}..."
    show her forlorn
    me "Thanks, I think?"
    "She takes a moment, drawing a deep breath and composing Herself."
    show her neutral talk
    her "So{w} this is a time loop. This day has played out many times before."
    show her neutral
    me "Wow, like... dozens of times?"
    show her evasive talk
    her "Oh...{w} sweetie no..."
    show her evasive
    "I wait for Her to clarify, as She nervously avoids My gaze."
    me "Not {i}hundreds{/i} surely??"
    show her evasive talk
    her "...thousands."
    show her evasive

    me "THOUSANDS!?" with vpunch
    me "You've been forced to live through this day that many times?"
    "She steps closer {st}to Me, Her hand gripping My shoulder tight.{w=0.1} I see the longing in Her eyes, the one We both ignore, or try to.{w=0.1} My question is already answered when She sighs {/st}and says."
    show her anxious talk
    her "Well, it usually doesn't last the whole day.{w} It only resets to Us on the hill, talking. Right when that com{st}ph{/st}et {st}agent{/st} lands."
    her "And well... it often doesn't get far. Someone cuts the day prematurely short, and everything resets again."
    show her anxious
    me "Is that the someone who's trying to kill Us?{w} But... who??{w} Who would want to do that to Me???"

    scene black
    stop music
    $ stop_layers(0)
    play sound "audio/tick tock.wav" loop fadein 2.0

    n "The air whooshes past My ear.{w}\nThis time, I can finally see what it is.{w}\nA long thin metal object, a throwing knife."
    n "It has a strange round end, it reminds me of something...{w}\nI can see it fine this time, because it's perfectly still."
    n "Sticking out of Her stomach."
    nvl clear

    scene river
    show her forlorn at centre
    with Pause(0.3)

    show her forlorn:
        subpixel True
        xpos 540
        ypos 0
        ease 0.6 ypos 1044
    with Dissolve(0.5)
    with Pause(0.7)
    hide her

    $ stop_layers(3)
    "She falls back,{w} but before I can scramble to reach for Her, I am dragged backwards,{w} I fall as I look up{w} at a striking figure now looking down on Me."
    "I stare at the tall, imposing, oddly {w}{i}familiar{/i} person."
    hide her

    queue sound "audio/tick tock.wav"
    play music "audio/music/MeNotMe.wav" loop fadein 5.0
    show not_me neutral at right with Dissolve(5)

    me "...Me?"
    show not_me angry talk
    not_me "Not You, never You."
    show not_me neutral:
        subpixel True
        ease 0.05 xpos 1080
        ease 0.35 xpos 540
    $ add_achievement("not_me")

    "Not Me flicks a wrist,{w} suddenly holding another one of those knives in Not My hand."
    "Squinting, I now can see what the shape reminds Me of."
    "It was a clock hand, but long and sharp.{w} Not Me pulls Not My hand away, as My attention flicks back to Her instead."

    show not_me angry talk
    not_me "{i}Unless{/i} You can cop on right now, You cannot become me.{w} You're straying from the path."
    not_me "But You {b}can{/b} change.{w} i became Not You, so it's obvious that You can too."
    show not_me angry
    "Not Me points the hand knife directly at Me, frustration and anger showing through Not My attempt to seem detached."
    "Not Me looks so much older.{w} No, not{w} older...{w} Aged."

    show not_me neutral talk
    not_me "Everyone's given You enough chances, it's time You just shut up and understood.{w} Take the path before You."
    show not_me neutral
    me "What? I don't understand-"

    show not_me neutral:
        subpixel True
        parallel:
            xpos 540
            ease 0.05 xpos 630
            ease 0.35 xpos 49
            ease 0.05 xpos -40
        parallel:
            ypos 0
            ease 0.05 ypos 0
            ease 0.35 ypos -720
        parallel:
            zoom 1.0
            ease 0.05 zoom 1.0
            ease 0.35 zoom 2.0
    #show not_me neutral:
        #pos (-18, -720) zoom 2.0

    "Not Me moves so fast, and doesn't even make a sound as Not Me does.{w} Before I know it, Not Me lifts Me by My neck."
    camera:
        subpixel True
        ypos 0
        ease 0.80 ypos 620

    "The tips of My toes stay just barely on the ground as I strain to keep Myself upright."
    show not_me neutral talk
    not_me "That's right, You don't understand.{w} But i do.{w} Stop running, dodging and deflecting."
    show not_me neutral

    camera:
        subpixel True
        xpos 0
        ease 0.15 xpos -81
        ease 0.07 xpos 252
    with Pause(0.05)

    scene bridge with None
    camera:
        xpos 0 ypos 0
    with punch3

    "Not Me tosses My body back, and I fall on the ground behind.{w} But Not Me wastes no time advancing to loom over Me once more."

    show not_me angry talk at centre

    camera:
        subpixel True
        ypos 0
        ease 0.17 ypos 171
    with Pause(0.17)

    camera:
        subpixel True
        ypos 171
        ease 0.06 ypos 0
    with punch4

    not_me "Accept the path laid out before You,{w} the only way forward for You.{w} It's always been meant for You."
    show not_me neutral
    "My heart pounds as Not Me speaks words I have been afraid to confront."
    me "you mean..."
    show not_me neutral talk
    not_me "Yes."
    not_me "You must{w} become {i}His{/i} girlfriend."
    show not_me neutral
    me ""
    me "THAT'S My {i}destiny{/i}?? you've got to be fucking kidding Me."
    play sound "audio/stab.wav" volume 3.0
    "Not Me crouches low, as Not My knife digs into My cheek, proving that Not Me was not fucking kidding."
    show not_me angry talk
    not_me "This is not a game little girl.{w} You're hurting Yourself.{w} But You're also hurting Him.{w} And You're hurting Her."
    show not_me angry

    "I glance backwards towards Her bleeding on the ground. Not dead thankfully,"
    with vpunch
    "But before I even make a move, Not Me throws Me flat onto My back in frustration."

    show not_me angry talk
    not_me "{i}Forget Her.{/i}{w} It's for the best."
    show not_me angry
    "I swallow, and open My mouth to answer as Not My steely gaze bears down on Me."
    show not_me neutral talk
    not_me "It's a simple yes or no question.{w} Will You finally just go with Him?"
    show not_me neutral

    $ rejection_count = 0
    jump choose

label choose:
    $ rejection = ""
    if rejection_count == 0:
        $ rejection = _("No.")
        if _preferences.language == "chinese":
            $ rejection = ("不行。")
    elif rejection_count == 1:
        $ rejection = _("Fuck no.")
        if _preferences.language == "chinese":
            $ rejection = _("不行，滚。")
    elif rejection_count >= 2:
        $ rejection = _("I choose Her.")
        if _preferences.language == "chinese":
            $ rejection = _("我选[她]。")

    menu:
        "{affect}Yes{/affect}" if persistent.affectation > 15 and rejection_count == 0:
            $ adjust_affectation(25)
            if persistent.affectation < 50:
                show not_me neutral talk
                not_me "You fought so hard before now, i'm going to need You to declare it again."
                show not_me neutral
                jump choose
            elif persistent.affectation < 75:
                show not_me neutral talk
                not_me "Good, good. But no wavering.{w} You have to stick to the path.{w} There's no point in You straying away again."
                show not_me neutral
                jump choose
            elif persistent.affectation < 100:
                show not_me neutral talk
                not_me "i almost believe You.{w} But {i}almost{/i} is not enough."
                not_me "You need to give more than 100\%, so no-one else will have any doubts about You and Him."
                show not_me neutral
                jump choose
            else:
                show not_me neutral talk
                not_me "Good, finally.{w} my part is done. Forget Her, forget me, forget today."
                not_me "Now it's just You and Him.{w} He's right over there, waiting."
                show not_me neutral
                hide not_me with dissolve
                "My head obediently follows where Not Me points, and I do see Him approaching.{w} I turn back towards Not Me, but I don't need to make up something to say because there's nobody there."
                "I know I can't brave facing Her,{w} so I turn to {w=0.1}Him."
                jump false_end

        "{st}[rejection]{/st}" if persistent.affectation < 100:
            $ adjust_affectation(-35)
            $ rejection_count += 1
            if rejection_count == 1:
                show not_me nervous talk
                not_me "i will give You another chance because i'm rather tired of killing You."
                show not_me nervous
                jump choose
            elif rejection_count == 2:
                show not_me nervous talk
                not_me "Think carefully now, You don't really have any options but Him."
                show not_me nervous
                jump choose
            elif rejection_count >= 3:
                show not_me nervous talk
                not_me "If You are this beyond help i'll just have to kill You, start over and try again."
                show not_me nervous

    "Not Me laughs ruefully as Not Me shakes Not My head."
    show not_me angry talk
    not_me "It seems You really are too far gone. i'm glad i was never this beyond help."

    window auto hide
    show not_me angry talk:
        subpixel True
        parallel:
            xpos 540
            ease 0.3 xpos 306
        parallel:
            ypos 0
            ease 0.3 ypos -243
        parallel:
            zoom 1.0
            ease 0.3 zoom 1.39

    "I try to make a move but Not Me has already closed the distance, interrupting My clumsy steps forward."
    show not_me angry talk
    not_me "How disappointing,{w} that You'd fail to assume the path laid out for You."
    show not_me angry
    with punch5
    "Not Me redirects My trajectory, flipping Me onto My back. I'm winded as Not Me looms over Me triumphantly."
    show not_me neutral talk
    not_me "And for what? You know how this ends,{w=0.5} it's already decided. Your inevitable fate."
    me "Hah..."

    show not_me angry talk:
        subpixel True
        ypos -243
        ease 0.38 ypos -99

    not_me "Excuse {i}You{/i}, are You {b}laughing{/b}?"
    show not_me angry
    "Standing over Me, Not Me's confidence is oddly shaken as I recover my breath."
    me "Ahuh, I realised why- why someone would try {b}this{/b} hard to stop Me.{w} Can't believe it took this long,{w} it's desperation."

    show not_me nervous talk
    not_me "Desperation!? Ha-"
    show not_me nervous
    me "Yep, desperation to {i}change{/i} what's going to happen. Because it's not predetermined.{w} That's what's scary. Fate would be simpler, then it'd all be picked for Me."
    show not_me nervous talk
    not_me "You can't choose t-"
    me "No, you're wrong.{w} It's Me who {b}can{/b} choose, but Not You."
    me "you're Not Me{w} ...you don't even exist. {w}you never did, {w=0.5}did you?"
    stop music fadeout 3.0
    hide not_me
    pause

    me "Don't worry, it'll be worth it."
    "I speak to the air. I can feel a tension, either from Me or Not Me. It doesn't matter when I shake it off and run to Her side."
    nvl clear

    play music "audio/music/TrueEnd.wav" loop fadein 5
    scene river
    show her anxious at centreclose
    with Dissolve(2.0)

    n "She lies wounded.{w} It's bad, it's messy."
    extend "\nIt'd be better if I had figured everything out sooner.{w} Committing instead of just pretending I didn't want anything."
    n "Now She's hurt and it's all just a mess.{w}\nA mess made by Me{w} and Not Me."
    extend "\nSo it's time I clean it up."
    n "I put My hand on Her chest, I feel the heartbeat that reassures Me,{w} She is still there."
    n "Her hand weakly clutches at My cheek and She smiles."
    nvl clear

    show her anxious talk
    her "Huh..."
    show her joy talk
    extend " never got this far before..."

    hide screen affect_meter

    if _preferences.language == "chinese":
        $ title = " 真\n结局"
    else:
        $ title = _("True\n   End")
    show screen ending(title)
    scene black
    with Dissolve(3)

    me "Me neither."
    pause

    hide screen ending
    scene title_screen_logoless
    play sound "audio/whitenoise.wav"
    show coupleB:
        subpixel True additive 0.0 xpos 0
        ypos -1090 blur 30.0
        ease 0.17 ypos 63 blur 20.0
    with Pause(0.17)
    show coupleB:
        subpixel True additive 0.0 xpos 0
        ypos 63 blur 20.0
        ease 0.03 ypos 0 blur 0.0
    with punch1
    with Pause(0.13)
    show coupleB:
        pos (0, 0) blur 0.0


    show coupleG:
        subpixel True additive 0.0 xpos 0
        ypos -1090 blur 30.0
        ease 0.17 ypos 63 blur 20.0
    with Pause(0.17)
    show coupleG:
        subpixel True additive 0.0 xpos 0
        ypos 63 blur 20.0
        ease 0.03 ypos 0 blur 0.0
    with punch2
    with Pause(0.13)
    show coupleG:
        pos (0, 0) blur 0.0

    show coupleR:
        subpixel True additive 0.0 xpos 0
        ypos -1090 blur 30.0
        ease 0.17 ypos 63 blur 20.0
    with Pause(0.17)
    show coupleR:
        subpixel True additive 0.0 xpos 0
        ypos 63 blur 20.0
        ease 0.03 ypos 0 blur 0.0
    with punch3
    with Pause(0.13)

    hide coupleR
    hide coupleG
    hide coupleB
    show couple:
        pos (0, 0) blur 0.0
    with None

    stop sound
    pause

    hide window
    n "Art by @esshemasha on twitter."
    extend "\nWriting, programming and music by SuperBiasedGary."

    if _preferences.language == "chinese":
        n "\n本地化："
        extend "\nWarlocs 项目管理人员：{b}Michael Stein{/b}"
        extend "\n简体中文翻译：{b}Hannah Lu{/b}"
    else:
        n "\nLocalisation:"
        extend "\nWarlocs Project Management: {b}Michael Stein{/b}"
        extend "\nSimplified Chinese: {b}Hannah Lu{/b}"

    n "\n{b}Special Thanks:{/b}\n{w=0.3}{nw}"
    extend "{b}Littlerat{/b} for playtesting and feedback.\n{w=0.5}{nw}"
    extend "{b}Travis{/b} for playtesting and feedback.\n{w=0.5}{nw}"

    n "{b}Josh Loomis{/b} for playtesting and feedback.\n{w=0.5}{nw}"
    extend "{b}Tim White{/b} for playtesting, feedback and mentoring.\n{w=0.5}{nw}"
    extend "{b}Karleen Heong{/b} whose Game Music Composition course was instrumental in making this soundtrack.{w=0.5}{nw}"

    n "{b}Pexels.com{/b} for stock photography.\n{w=0.5}{nw}"
    extend "The {b}Sonniss GDC Sound Effects Libraries.{/b}{w=0.5}{nw}"
    extend "\nThe {b}Game Dev Galaxy{/b} community for their support."

    n "Thanks for playing!"

    $ persistent.true_end = True
    $ add_achievement("end_true")
    nvl clear
    return

label dead_end1:
    stop music fadeout 5.0
    $ stop_layers(5.0)
    play sound "audio/tick tock.wav" loop fadein 5.0

    show her anxious at centre
    "Her eyes widen, and She {st}must feel My heartbeat quicken, just like I can feel Hers against Me while She{/st} makes direct eye contact."
    "Her gaze pleads with Me.{w} As I try to unpack what's going on, She just shakes Her head."
    show her anxious talk
    her "Fuck... why did You have to wait to be so certain {i}now{/i}?{w} After..."

    hide screen affect_meter with None
    scene black

    stop sound
    play sound "audio/stab.wav" volume 3.0
    pause 1.0
    play music "<loop 8>audio/music/DeadEnd.wav"
    pause 1.5
    scene space with Dissolve(3.0)

    "As the sound rings out, it takes only an instant for the world to disappear."
    "My hearing lingers just enough for Her whispered sobs to come through,{w} but I can't make out any of Her words."
    "Instead... there's another voice.{w} Crisp and clear."

    show not_me angry silhouette:
        subpixel True xpos 25 ypos -828 zoom 3.0

    not_me "Enough of Your melodramatic indecision.{w} Delaying the inevitable answer just hurts everyone else."

    show not_me:
        subpixel True
        parallel:
            xpos 25
            ease 0.8 xpos -1475
    with Pause(0.5)
    show not_me:
        pos (-1475, -828)

    not_me "If You actually wanted anything different,{w} You wouldn't have agonised over it so long."

    if _preferences.language == "chinese":
        $ title = "死亡\n结局\n#1"
    else:
        $ title = _("DEAD\n END\n  #1")

    show screen ending(title)
    extend " So just accept where everyone else knows this is all going."
    window hide dissolve

    show not_me:
        subpixel True
        parallel:
            xpos -1475
            easeout 1.0 xpos -2835

    $ add_dead_end(1)
    show noise:
        blend "add"

    pause 2.5
    pause 2.5
    stop sound
    stop music
    $ stop_layers()
    jump start

label dead_end2:
    stop music fadeout 5.0
    $ stop_layers(5.0)
    stop sound
    play sound "audio/tick tock.wav" loop fadein 5.0
    scene hill

    show her anxious at centreclose with dissolve
    "She pounces without warning, and My heart flutters{w} even as She slams My head against the railing."
    me "FUCK"
    show her anxious talk
    her "No time, listen-"
    show her anxious
    me "No seriously, what was that for?"
    show her anxious talk
    her "We {b}don't{/b} have time! For once,{w} You have to hurr-{nw}"
    play audio "audio/stab.wav"

    show her anxious talk:
        subpixel True
        parallel:
            ypos -355
            linear 0.07 ypos -398
            easein_circ 0.29 ypos -398
            linear 0.97 ypos 972
        parallel:
            alpha 1.0
            linear 0.85 alpha 1.0
            linear 0.80 alpha 0.0
        parallel:
            matrixcolor InvertMatrix(0.0)*ContrastMatrix(1.0)*SaturationMatrix(1.0)*BrightnessMatrix(0.0)*HueMatrix(0.0)
            linear 0.36 matrixcolor InvertMatrix(0.0)*ContrastMatrix(1.0)*SaturationMatrix(1.0)*BrightnessMatrix(0.0)*HueMatrix(0.0)
            linear 0.67 matrixcolor InvertMatrix(0.0)*ContrastMatrix(1.0)*SaturationMatrix(0.0)*BrightnessMatrix(0.0)*HueMatrix(0.0)
    with Pause(1.75)
    show her anxious talk:
        ypos 972 alpha 0.0 matrixcolor InvertMatrix(0.0)*ContrastMatrix(1.0)*SaturationMatrix(0.0)*BrightnessMatrix(0.0)*HueMatrix(0.0)

    "She slumps over limp, red blood tricking down Her chest as I struggle to process what's happening."
    "I scramble backwards hurriedly,{w} and then I notice{w} something...{w} sticking out of Her motionless back."
    "What...{w} is that..."
    "Some sort of knife?"
    "But it's shaped like{nw}"

    stop sound
    play sound "audio/stab.wav" volume 3.0
    scene black
    pause 1.0
    "But it's shaped like{fast}-"
    play music "<loop 8>audio/music/DeadEnd.wav"
    pause 1.5


    scene space with Dissolve(3.0)

    "Having wasted the moments She bought Me, I slump forward onto Her, My senses fading away."
    "My consciousness dissolves,{w} while I am subsumed by embarrassment{w} when Our hands accidentally touch."

    hide screen affect_meter
    if _preferences.language == "chinese":
        $ title = "死亡\n结局\n#2"
    else:
        $ title = _("DEAD\n END\n  #2")
    show screen ending(title)
    show not_me angry silhouette:
        subpixel True zoom 3.0 ypos -1008
        parallel:
            xpos -2500
            ease 0.80 xpos -1485

    not_me "Now you've gone and ruined Her life too.{w} Still think it was worth it?"

    show not_me angry silhouette:
        subpixel True zoom 3.0
        parallel:
            ypos -1008
            ease 0.80 ypos -2691

    not_me "You can't just opt out of doing anything forever.{w} Sooner or later You have to act,{w} so I did."
    window hide dissolve

    show not_me angry silhouette:
        subpixel True zoom 3.0
        parallel:
            xpos -1485
            ease 0.8 xpos -3564

    pause 0.8

    $ add_dead_end(2)
    show noise:
        blend "add"

    pause 2.5
    pause 2.5
    stop sound
    stop music
    $ stop_layers()
    jump start

label dead_end3:
    stop music fadeout 5.0
    $ stop_layers(5.0)
    stop sound
    play sound "audio/tick tock.wav" loop fadein 5.0

    me "Me up! I mean"
    me "You need a Me"
    show her joy
    me "I mean,"
    me "Pick Me up."
    "She blinks at Me."
    me "You, Me, need, pick, up."
    "My palm sweats enough that I slip out of Her grasp and She giggles,{w} and reaches to grasp-{nw}"

    stop sound
    play sound "audio/stab.wav" volume 3.0
    show her anxious

    "My palm sweats enough that I slip out of Her grasp and She giggles, and reaches to grasp-{fast}"
    pause 1.0
    play music "<loop 8>audio/music/DeadEnd.wav"
    pause 1.5

    scene space with Dissolve(2.0)

    "Her grasp is moments too late. Someone else- {cps=*5}something else{/cps}, has already touched Me."
    "And killed Me."
    "{cps=*.1}...{/cps}I can't believe those were My last words."

    hide screen affect_meter

    if _preferences.language == "chinese":
        $ title = "死亡\n结局\n#3"
    else:
        $ title = _("DEAD\n END\n  #3")
    show screen ending(title)

    show not_me angry silhouette:
        subpixel True ypos -3222 zoom 2.0
        xpos -1935
        ease 1.19 xpos 27

    not_me "You are so embarrassing, i can't believe it.{w} Eugh, You make my skin crawl."
    extend " Shocking that you imagine anyone could find this {i}charming{/i}."
    window hide dissolve

    show not_me angry silhouette:
        subpixel True xpos 27 ypos -3222 zoom 2.0
        ease 0.72 xpos 2651

    pause 0.8

    $ add_dead_end(3)
    show noise:
        blend "add"

    pause 2.5
    pause 2.5
    stop sound
    stop music
    $ stop_layers()
    jump start

label dead_end4:
    stop music fadeout 5.0
    $ stop_layers(5.0)
    stop sound
    play sound "audio/tick tock.wav" loop fadein 5.0

    show her anxious
    "She freezes, staring at Me. Terror in Her eyes as She shakes Her head."
    show her anxious talk
    her "No... why now? It's already too late.{w} Up on the hill, that was your last {i}chance{/i}."
    show her anxious
    "I get nervous immediately, worried I've said too much and I quickly try to backtrack. "
    me "What? ...that was just a joke! Hah, can't believe I got You."
    "I've clearly just embarrassed Myself and Her.{w} She'd never be fooled by one of My cheap lies."
    "She sees right through Me.{w=0.3} Or,{w} now She seems to be looking past Me..."
    "She shoves Me suddenly aside, stepping forward and She inhales sharply.{w} I instinctively grab Her arm, assuming She's trying to run away."
    "No,{w} wasn't I the one who chickened out?"

    "I want to stop Her,{w}\nAnd My hand feels"
    stop sound
    play sound "audio/stab.wav" volume 3.0
    show her anxious at centreclose
    "I want to stop Her,\nAnd My hand feels{fast}... wet{w}\nas red liquid seeps through My fingers."

    show her anxious talk:
        subpixel True
        matrixcolor InvertMatrix(0.0)*ContrastMatrix(1.0)*SaturationMatrix(1.0)*BrightnessMatrix(0.0)*HueMatrix(0.0)
        ease 5.0 matrixcolor InvertMatrix(0.0)*ContrastMatrix(1.0)*SaturationMatrix(0.0)*BrightnessMatrix(0.0)*HueMatrix(0.0)

    her "Run."

    me "Are you ok-"

    her "Forget about Us and {b}RUN{/b}{w} before Not You kills You.{w} Not You won't stop,{w} You don't-"

    "This time I can see, it's a"
    play sound "audio/stab.wav" volume 3.0
    "This time I can see, it's a{fast}... a knife?{w} It's thrown directly into Her back as She pleads up at Me."

    show her anxious talk:
        subpixel True
        matrixcolor InvertMatrix(0.0)*ContrastMatrix(1.0)*SaturationMatrix(0.0)*BrightnessMatrix(0.0)*HueMatrix(0.0)
    hide her with Dissolve(2.0)

    "She stepped in to protect Me.{w=0.5} Protect Me from what?"

    show not_me angry silhouette:
        zoom 2.0
        pos (135, -2853)
    with dissolve

    "I look up, and I see a stern, disapproving and tired face."

    show not_me angry silhouette:
        subpixel True xpos 135 zoom 2.0
        ypos -2853
        ease 2.50 ypos -45

    "For a moment,{w=0.5} I kid Myself{w=0.5} and think it's My mother."
    "But then I admit it."

    show not_me angry silhouette:
        pos (135, -45)

    me "Me?"
    not_me "Not You"
    "I don't even notice when Not My hand blurs,{nw}"

    play sound "audio/stab.wav" volume 3.0
    scene black with None
    scene space with Dissolve(3.0)
    pause 1.0

    play music "<loop 8>audio/music/DeadEnd.wav"
    "I don't even notice when Not My hand blurs,{fast} flinging a long thin knife into My neck."
    pause 1.5


    "Not Me chides Me, as My vision fades away."

    hide screen affect_meter

    if _preferences.language == "chinese":
        $ title = "死亡\n结局\n#4"
    else:
        $ title = _("DEAD\n END\n  #4")
    show screen ending(title)
    not_me "Not You. Never You."
    extend " I made the {b}right{/b} choices. You so easily could have."
    window hide dissolve

    $ add_dead_end(4)
    show noise:
        blend "add"

    pause 2.5
    pause 2.5
    stop sound
    stop music
    $ stop_layers()
    jump start

label dead_end5:
    stop music fadeout 5.0
    $ stop_layers(5.0)
    stop sound
    play sound "audio/tick tock.wav" loop fadein 5.0

    show her forlorn talk
    her "Please, You have to come {i}on{/i}.{w} We were nearly home free, You can't just tease like that..."
    show her forlorn
    me "You have to tell Me what's going on before I'm going {i}anywhere{/i}.{w} Is this some elaborate prank with Him?"
    show her forlorn talk
    her "No! Forget about Him."
    show her anxious
    "There are tears on Her face now... She never cries.{w} Or, She never lets Me see Her cry."
    "She scrambles back up over the railing once more, reaching for My hand.{w} But Her panic starts to worry Me."
    me "Uhhh, I thought you said He was a nice guy?"
    show her anxious talk
    her "Yeah a 'nice guy', {i}derogatory{/i}.{w} Can't You read between the lines..."
    show her anxious
    extend "\nDon't say that You {b}actually{/b} like Him?"
    "She clams up, as I pull My hand away from Her."
    me "Him? Not a chance, I'd sooner die t{nw}"

    stop sound
    play sound "audio/stab.wav" volume 3.0
    me "Him? Not a chance, I'd sooner die t{fast}{nw}"
    scene black with None
    scene space with Dissolve(3.0)
    pause 1.0

    play music "<loop 8>audio/music/DeadEnd.wav"
    pause 1.5
    extend "-"

    hide screen affect_meter

    if _preferences.language == "chinese":
        $ title = "死亡\n结局\n#5"
    else:
        $ title = _("DEAD\n END\n  #5")
    show screen ending(title)
    show not_me neutral silhouette at leftclose with Dissolve(3.0)
    not_me "Well, it's Him or death."

    show not_me neutral silhouette:
        subpixel True
        pos (54, -300) xzoom 1.5 zoom 1.0
        linear 0.69 pos (-135, -140) xzoom 1.5 zoom 1.25
    extend " Consider it more carefully next time."
    window hide dissolve

    $ add_dead_end(5)
    show noise:
        blend "add"

    pause 2.5
    pause 2.5
    stop sound
    stop music
    $ stop_layers()
    jump start
